buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "maven-publish"
apply plugin: 'signing'

group 'com.github.isyscore'
version '2.0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    api "com.github.isyscore:common-jvm:2.0.1"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    api 'me.friwi:jcefmaven:110.0.25'
    api 'com.formdev:flatlaf:3.0'
    api 'com.jtattoo:JTattoo:1.6.13'
    api 'org.apache.xmlgraphics:batik-swing:1.17'
    testImplementation 'com.fifesoft:rsyntaxtextarea:3.3.2'
    testImplementation "junit:junit:4.13.2"
}

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenKotlin(MavenPublication) {
            groupId = "com.github.isyscore"
            artifactId = "common-swing"
            version = "2.0.1.0"
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "common-swing"
                description = "iSysCore Common Swing Library"
                url = "https://github.com/isyscore/common-swing"
                packaging = "jar"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/isyscore/common-swing/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "isyscore"
                        name = "isyscore"
                        email = "hexj@isyscore.com"
                    }
                }
                scm {
                    connection = "https://github.com/isyscore/common-swing"
                    developerConnection =  "https://github.com/isyscore/common-swing"
                    url = "https://github.com/isyscore/common-swing"
                }
            }
        }
    }
    repositories {
        maven {
            name = "Release"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = "$nexusUsername"
                password = "$nexusPassword"
            }
        }
        maven {
            name = "Snapshot"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = "$nexusUsername"
                password = "$nexusPassword"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenKotlin
}
